plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id 'jacoco'
    id "io.gitlab.arturbosch.detekt"
}

apply from: "signing.gradle"
apply from: "dependencies.gradle"
apply from: "test-dependencies.gradle"
apply from: "${rootProject.projectDir}/detekt.gradle"

android {
    compileSdk 33
    namespace 'com.infineon.smack.sample'

    defaultConfig {
        applicationId "com.infineon.smack.sample"
        minSdk 21
        targetSdk 33
        versionCode System?.env?.CI_PIPELINE_ID?.toInteger() ?: 1
        versionName projectVersion

        testInstrumentationRunner "de.xroot.android.HiltTestRunner"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.ci
            testCoverageEnabled true
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.production
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    flavorDimensions "default"
    productFlavors {
        beta {
            applicationIdSuffix ".beta"
            versionNameSuffix " beta"
        }
        production {
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
        kotlinOptions.freeCompilerArgs += [
                "-Xopt-in=kotlin.RequiresOptIn",
                "-Xopt-in=kotlin.ExperimentalUnsignedTypes",
                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }
    packagingOptions.resources.excludes += ['META-INF/AL2.0', 'META-INF/LGPL2.1']

    testOptions {
        unitTests.returnDefaultValues = true
    }
}
